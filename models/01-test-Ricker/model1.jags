model{
  for(i in 1:N){
    # Likelihood
    et[i] ~ dnorm(mu[i], tau)
    # Replicated data
    et.rep[i] ~ dnorm(mu[i], tau)
    
    # Mean model
    # KO: Here's an alternative that is reparameterized in terms of "meaningful" parameters and first evaluated on the Log-scale:
    mu[i] <- y.peak[pID[i]]*TimePart[i]
    TimePart[i] <- exp(LogPart[i])
    LogPart[i] <- 1-(t[i]/t.peak[pID[i]]) + log(t[i]) - Lt.peak[pID[i]]
    
    # Square differences
    Sqdiff[i] <- pow(et[i] - et.rep[i], 2) 
  }
  
  for(p in 1:Npulse){ # number of pulses
    Lt.peak[p] ~ dnorm(mu.Lt.peak[sID[p]], tau.Lt.peak)
    t.peak[p] <- exp(Lt.peak[p])
    y.peak[p] ~ dnorm(mu.y.peak[sID[p]], tau.y.peak)
  }
  
  # Priors for Ricker model and variance
  for(s in 1:Nstudy){ # number of studies
  # KO: I would model the parameters hierarchically. The reparmeterized model is in terms
  # of the time (t) at which the response peaks (t.peak) and the maximum response value at
  # the peak (y.peak)
    # KO: Priors on log-scale:
    mu.Lt.peak[s] ~ dnorm(M.Lt.peak, T.Lt.peak)
    mu.t.peak[s] <- exp(mu.Lt.peak[s])
    mu.y.peak[s] ~ dnorm(M.y.peak, T.y.peak)
    
    
}
# KO: priors for population level parameters:
    M.Lt.peak ~ dnorm(0,0.0001)
    M.y.peak ~ dnorm(0,0.0001)
    M.t.peak <- exp(M.Lt.peak)
    
    
    # pulse level
    sig.Lt.peak ~ dunif(0,100)
    sig.y.peak ~ dunif(0,100)
    tau.Lt.peak <- pow(sig.Lt.peak,-2)
    tau.y.peak <- pow(sig.y.peak,-2)
    
    # study level
    S.Lt.peak ~ dunif(0,100)
    S.y.peak ~ dunif(0,100) 
    T.Lt.peak <- pow(S.Lt.peak, -2)
    T.y.peak <- pow(S.y.peak, -2)
    
  
  # Observation level
  tau ~ dgamma(0.01, 0.01)
  sig <- pow(tau, -0.5)
  
  # Dsum
  Dsum <- sum(Sqdiff[])
}
